buildscript {
    repositories {
    	// Maven Central for resolving dependencies
        mavenCentral()
    }
    dependencies {
    	// https://plugins.gradle.org/plugin/org.springframework.boot
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
    }
}

// https://docs.gradle.org/current/samples/sample_building_java_libraries.html

plugins {
	// Apply the java-library plugin for API and implementation separation
    id 'java-library'
    id 'maven-publish'
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "jacoco"


bootJar {
    baseName = 'tourGuide'
    version =  '1.0.0'
}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

// https://stackoverflow.com/questions/23796404/could-not-find-method-compile-for-arguments-gradle
/* Note that the compile, runtime, testCompile, and testRuntime configurations 
 introduced by the Java plugin have been deprecated since Gradle 4.10 (Aug 27, 2018),
and were finally removed in Gradle 7.0 (Apr 9, 2021).
The aforementioned configurations should be replaced by implementation,
 runtimeOnly, testImplementation, and testRuntimeOnly, respectively.
 http://man.hubwiz.com/docset/Gradle_User_Guide.docset/Contents/Resources/Documents/userguide/java_library_plugin.html
*/
dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    
    implementation group: 'org.javamoney', name: 'moneta', version: '1.3'
	implementation group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'

	// Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation(name:'gpsUtil', ext:'jar')
    implementation(name:'RewardCentral', ext:'jar')
    implementation(name:'TripPricer', ext:'jar')
    
    // Junit
    testImplementation("junit:junit")
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '2.1.6.RELEASE'
    
    // Use JUnit Jupiter for testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    
}

// Jacoco
jacoco {
    toolVersion = "0.8.4"
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

test.finalizedBy jacocoTestReport
check.dependsOn jacocoTestCoverageVerification

jacocoTestCoverageVerification {
  violationRules {
    rule {
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.5
      }
    }
  }
}
